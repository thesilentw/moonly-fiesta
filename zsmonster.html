<html>
<head>
	<style>
	pre code {
		font-family: monospace;
		font-size: larger;
		background-color: #ddd;
		display: block;
		padding: 1em;
	}
	html {
		font-family: sans-serif;
	}
	</style>
</head>

<title>How 2 Make Monster Good (Also in ZScript)</title>
<h1>How 2 Make Monster Good (Also in ZScript)</h1>

<p>Welcome back, delicious friends, to the next installment of <strong>HOW 2</strong>, <strong>HOW 2 2: HEY NOW, YOU'RE A MONSTAR</strong>.<br><br>
As the previous one did, this tutorial assumes several things:
</p>
<ul>
<li>You did (or at least read) the previous tutorial - HOW 2 GNU BUT WITH ZSCRIPT</li>
<li>You have created a monster for GZDoom, using DECORATE (or at least looked at the code for one).</li>
<li>You have overcome (or at least come to terms with) <em>the fear</em> of ZScript for weapons...
<li>...But now you want to make monsters, and it's like your first day of High School all over again, except it's Cacodemons and Barons all laughing at you and then they steal your keyboard and stuff you in a broom closet full of spaghetti.</li>
</ul>
<p>
Assuming (some of) the above is true, we will begin with a historical artifact, as this has <em>technically</em> never existed in the GZDoom source - the DECORATE code for the standard Imp.</p>
<pre><code>
	ACTOR TutorialImp {
		Health 60
		Radius 20
		Height 56
		Mass 100
		Speed 8
		PainChance 200
		Monster
		+FLOORCLIP
		SeeSound "imp/sight"
		PainSound "imp/pain"
		DeathSound "imp/death"
		ActiveSound "imp/active"
		HitObituary "%o was shredded by a tutorial imp!"
		Obituary "%o was BBQed by a tutorial imp!"

		States {
			Spawn:
				TROO AB 10 A_Look
				Loop
			See:
				TROO AABBCCDD 3 A_Chase
				Loop
			Melee:
			Missile:
				TROO EF 8 A_FaceTarget
				TROO G 6 A_TroopAttack
				Goto See
			Pain:
				TROO H 2
				TROO H 2 A_Pain
				Goto See
			Death:
				TROO I 8
				TROO J 8 A_Scream
				TROO K 6
				TROO L 6 A_NoBlocking
				TROO M -1
				Stop
			XDeath:
				TROO N 5
				TROO O 5 A_XScream
				TROO P 5
				TROO Q 5 A_NoBlocking
				TROO RST 5
				TROO U -1
				Stop
			Raise:
				TROO ML 8
				TROO KJI 6
				Goto See
		}
	}

</code></pre>
<p>Right now, this uses the standard Imp's sprites - Replacing them is another matter. You can find plenty of fun replacements at <a href="https://www.realm667.com">Realm 667</a>.<br>
Take a minute to get this DECORATE version working before you move on. Confirm that everything is working as expected, or at least that you are aware of any existing issues.</p>
<h2>A-scripting We Shall Go...</h2>
<p>We shall now transmogrify this humble imp into... another humble imp in a different language.</p>
<h3>Actor Definition</h3>
<p>As before, we need to define this imp as a class. This time, though, we're subclassing Actor, not Weapon.
<pre><code>
class ZTutorialImp : Actor {

</pre></code>
</p>
<h3>Properties</h3>
<p>Next, we translate the properties of this imp into ZScript format. 
<pre><code>
	Default {
		Health 60;
		Radius 20;
		Height 56;
		Mass 100;
		Speed 8;
		PainChance 200;
		Monster;
		+FLOORCLIP
		SeeSound "imp/sight";
		PainSound "imp/pain";
		DeathSound "imp/death";
		ActiveSound "imp/active";
		HitObituary "%o was shredded by a tutorial imp!";
		Obituary "%o was BBQed by a tutorial imp!";
	}
	
</pre></code>
Common monster properties don't usually require much more than adding semicolons to the end of each line. As a quick refresher on the properties themselves:
<ul>
<li>Health is how much damage this monster can take before dying.</li>
<li>Radius is half of how many map units wide the actor is (radius * 2 = diameter, if you recall geometry).</li>
<li>Height is... how tall it is.</li>
<li>Speed is how many map units the monster moves during one step in A_Chase().</li>
<li>PainChance is the chance the monster will enter its pain state if hit, expressed as PainChance/256 (128 PainChance means a 50% chance to enter Pain state).</li>
<li>There are a <strong>LOT</strong> of other properties - <a href="https://zdoom.org/wiki/Actor_properties">check here for more info</a>. Their use (and abuse!) are left as an exercise for the reader.</li>
</ul>
</p>
<h3>States</h3>
<p>For monsters, states are how you create their AI - you can very easily see how an imp behaves if you look at its states.
<pre><code>
	States {
		Spawn:
			TROO AB 10 A_Look();
			Loop;
		See:
			TROO AABBCCDD 3 A_Chase();
			Loop;
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget();
			TROO G 6 A_TroopAttack();
			Goto See;
		Pain:
			TROO H 2;
			TROO H 2 A_Pain();
			Goto See;
		Death:
			TROO I 8;
			TROO J 8 A_Scream();
			TROO K 6;
			TROO L 6 A_NoBlocking();
			TROO M -1;
			Stop;
		XDeath:
			TROO N 5;
			TROO O 5 A_XScream();
			TROO P 5;
			TROO Q 5 A_NoBlocking();
			TROO RST 5;
			TROO U -1;
			Stop;
		Raise:
			TROO ML 8;
			TROO KJI 6;
			Goto See;
	}

</pre></code>
As before, our 'magic keywords' are translated into function calls. However, there are a few strange ones in here - like A_Look(), A_Chase(), A_TroopAttack(), and more.<br>
Monsters have a lot more 'shortcut' functions in their code, usually because the functionality there was ported from native code. Some of them can be replicated with ZScript, but some can't. Monsters, in general, require a little more research as the modder to get them to behave (We'll talk more about monster behaviors later).</p>
<h3>Sewing the patient up</h3>
<p>End the file with a closing brace ( } ), and don't forget to add your new TutorialImp.zsc to your  <strong>ZSCRIPT</strong> file:
<pre><code>
#include "subdirectory/ztutorialimp.zsc"

</code></pre>
</p>
<h3>Komm, s√ºsser Code.</h3>
<p>Launch GZDoom with our mod, and spawn our beautful baby imp: <strong>summon ztutorialimp</strong><br>
Let him kill you, every child should enjoy at least one kill before you blast them into bits of re-allocated memory.</p>
<h2>... I was promised eldrich... horrors? Can I go home?</h2>
<p>No, no, you can't go yet! We have Such Sights To Show You.
<pre><code>
	class ZSuperImp : Actor {
		Default {
			Health 666000;
			Radius 80;
			Height 240;
			Mass 1000;
			Speed 4;
			Scale 4;
			PainChance 0;
			Monster;
			+FLOORCLIP
			SeeSound "cyber/sight";
			PainSound "cyber/pain";
			DeathSound "cyber/death";
			ActiveSound "cyber/active";
			HitObituary "%o was erased by the great god imp.";
			Obituary "%o was purified.";
		}
	
		States {
			Spawn:
				TROO AB 10 A_Look();
				Loop;
			See:
				TROO AABBCCDD 3 A_Chase();
				Loop;
			Melee:
			Missile:
				TROO EF 8 A_FaceTarget();
				TROO G 6 A_SpawnProjectile("SuperImpBall", 150);
				Goto See;
			Death:
				TROO I 8;
				TROO J 8 A_Scream();
				TROO K 6;
				TROO L 6 A_NoBlocking();
				TROO M -1;
				Stop;
		}
	}
	
	class SuperImpBall : Actor {
		Default {
			Radius 32;
			Height 32;
			Speed 40;
			Damage 30;
			Scale 4;
			Projectile;
			+RANDOMIZE
			+ZDOOMTRANS
			RenderStyle "Add";
			Alpha 1;
			SeeSound "imp/attack";
			DeathSound "weapons/bfgx";
		}
		States {
			Spawn:
			BFS1 AB 4 Bright;
			Loop;
		Death:
			BFE1 ABC 8 Bright;
			BFE1 DEF 8 Bright A_Explode(256, 512);
			Stop;
		}
	}

</code></pre>
Give that a gander. I highly recommend saving it as <strong>zsuperimp.zsc</strong> and spawning it. Oh, and be sure to enable god mode.</p>
<p>As before, we'll step through the changes that turn the humble imp into the <em>GREAT GOD IMP</em>.
<pre><code>
	class ZSuperImp : Actor {
		Default {
			Health 666000;
			Radius 80;
			Height 224;
			Mass 1000;
			Speed 4;
			Scale 4;
			PainChance 0;
			Monster;
			+FLOORCLIP
			SeeSound "cyber/sight";
			PainSound "cyber/pain";
			DeathSound "cyber/death";
			ActiveSound "cyber/active";
			HitObituary "%o was erased by the great god imp.";
			Obituary "%o was purified.";
		}

</pre></code>
This Imp, of course, is named <em>SuperImp</em>. We've upgraded his Health (60 -> 666000), his size (Radius 20 -> 80, Height 56 -> 224), and his Mass (100 -> 1000). We've also decreased his Speed (8 -> 4), and increased his Scale (A multiplier for the size of his sprite, his actual size is still set with Radius and Height). He never feels pain (PainChance 0), and we've replaced all his sounds with the Cyberdemon's! We've also adjusted the obituaries to fit his new godlike status.</p>
<p></p><pre><code>
	States {
		Spawn:
			TROO AB 10 A_Look();
			Loop;
		See:
			TROO AABBCCDD 3 A_Chase();
			Loop;
		Missile:
			TROO EF 8 A_FaceTarget();
			TROO G 6 A_SpawnProjectile("SuperImpBall", 150);
			Goto See;
		Death:
			TROO I 8;
			TROO J 8 A_Scream();
			TROO K 6;
			TROO L 6 A_NoBlocking();
			TROO M -1;
			Stop;
	}

</pre></code>
For our Great God-Imp's states, we have actually simplified him considerably. He feels no pain, and so has no need for a Pain state. We've replaced <em>A_TroopAttack()</em> with a simple projectile shot - the Great God-Imp shall not dirty his claws with the blood of the puny. He still dies the same... assuming you can ever kill him!</p>
<p><pre><code>
	class SuperImpBall : Actor {
		Default {
			Radius 32;
			Height 32;
			Speed 40;
			Damage 30;
			Scale 4;
			Projectile;
			+RANDOMIZE
			+ZDOOMTRANS
			RenderStyle "Add";
			Alpha 1;
			SeeSound "imp/attack";
			DeathSound "weapons/bfgx";
		}
		States {
			Spawn:
			BFS1 AB 4 Bright;
			Loop;
		Death:
			BFE1 ABC 8 Bright;
			BFE1 DEF 8 Bright A_Explode(256, 512);
			Stop;
		}
	}

</pre></code>
Our Great God-Imp fires a rather different projectile than the usual one - We've mashed up the standard imp projectile with some things from the player's BFG, and set it to explode in a 512 unit radius when it hits the ground.</p>
<h2>Advanced AI Shenanigans</h2>
<p>For all his potent power, our Great God-Imp is really just a tremendous hammer. He hits hard, and takes a lot of punishment, but he's dumb as a doorstop. What if we made a smarter enemy? Weaker, but more devious?</p>

<h2>References</h2>
<ul>
<li><a href="https://github.com/marrub--/zscript-doc">The ZScript Documentation Project</a>. The best reference work for ZScript. Also the only one.</li>
<li><a href="https://zdoom.org/wiki/">The ZDoom Wiki</a>. Another good reference, but it's not especially well maintained or linked up - The ZScript resources are largely orphaned, improperly indexed, or poorly named. They do exist, though!</li>
<li><a href="https://github.com/coelckers/gzdoom/tree/master/wadsrc/static/zscript">The GZDoom Source Code</a>. When in doubt, go look at how the... maintainers... did it. I'm not going to tell you it's EASY to read, but it's the authoritative source for how to replicate vanilla behavior in ZScript.</li>
<li><a href="https://forum.zdoom.org/">The ZDoom Forums</a>, specifically the <a href="https://forum.zdoom.org/viewforum.php?f=43">Gameplay Mods</a> and <a href="https://forum.zdoom.org/viewforum.php?f=19">Other Projects</a> sections. The best reference is exploring a working version! I don't advocate copying others' code wholesale, but it can be extremely helpful to see how someone else solved a thorny problem, or created an effect you'd love to replicate.</li>
</ul>
<h2>Thanks</h2>
My own journey into ZScript would not have been possible without many others and the work they've done.


In no particular order, I wish to thank:
<ul>
<li><a href="https://greyserv.net/">Alison Watson</a> for the many, many hours of work they spent on their <a href="https://github.com/marrub--/zscript-doc">ZScript Documentation Project</a>.</li>
<li>iSpook, for getting me to write this. Hope it helps!</li>
<li>Yholl and Kegan, for their insistence that DECORATE is just fine, thank you. </li>
<li>Samarai1000, for your dedication to <em>gunfeel</em>.</li>
<li>Karla, for Project Ironclad, and for tolerating my ranting.</li>
<li>Any other denizens of the Secreta Lounge that I might have forgotten to acknowledge, for their inspiring work and assistance.</li>
</ul>
<br><br>
This work is licensed under a <a rel="license" href="http://creativecommons.org/licenses/by/4.0/">Creative Commons Attribution 4.0 International License</a>.
</html>
