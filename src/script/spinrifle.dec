ACTOR Spinrifle : Weapon {
	inventory.pickupMessage "Snagged a Spinner!"
	weapon.ammoType "Clip"
	weapon.ammoUse "1"
	weapon.slotNumber "4"
	weapon.ammogive "20"
	weapon.upsound "weapons/sprf_deploy"

	states {
		Spawn:
			TLGL A -1
			Stop
		
		Ready:
			TNT1 A 0 A_WeaponOffset(0, 32)
			SPRF A 1 A_WeaponReady
			Loop
		
		Select:
			SPRF A 1 A_Raise
			Loop
		
		Deselect:
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			SPRF A 1 A_Lower
			Loop
		
		Fire:
			SPRF A 1 A_WeaponOffset(0, 35, WOF_INTERPOLATE)
			SPRF B 2 A_WeaponOffset(0, 40, WOF_INTERPOLATE)
			TNT1 A 0 A_FireProjectile("SpinShot", 1, 1, 0.0, 9)
			TNT1 A 0 A_PlaySound("weapons/sprf_fire")
			SPRF C 4 A_WeaponOffset(0, 43, WOF_INTERPOLATE)
			SPRF B 4 A_WeaponOffset(0, 38, WOF_INTERPOLATE)
			SPRF A 3 A_ReFire("AutoFireStart")
			Goto Recovery
			
		Recovery:
			TNT1 A 0 A_StopSound(CHAN_WEAPON)
			SPRF B 3 A_WeaponOffset(0, 35, WOF_INTERPOLATE)
			SPRF A 2 A_WeaponOffset(0, 32, WOF_INTERPOLATE)
			Goto Ready
		
		AutoFireStart:
			TNT1 A 0 A_PlaySound("weapons/sprf_fireloop", CHAN_WEAPON, 1.0, true)
			Goto AutoFire
		
		AutoFire:
			SPRF C 2 A_WeaponOffset(0, 40, WOF_INTERPOLATE)
			TNT1 A 0 A_FireProjectile("SpinShot", 1, 1, 0.0, 9)
			SPRF D 2 A_WeaponOffset(0, 37, WOF_INTERPOLATE)
			SPRF C 1 A_WeaponOffset(0, 38, WOF_INTERPOLATE)
			TNT1 A 0 A_ReFire("AutoFire")
			Goto Recovery
	}
}

ACTOR SpinShot {
	Obituary "%o got spun right 'round  by %k's Spinner."
	Decal BulletChip
	Radius 5
	Speed 100
	Damage 10
	Gravity 1
	BounceFactor 0.99
	BounceCount 5
	DamageType None
	Scale 0.5
	Projectile
	Alpha 0.9
	BounceType Hexen
	DeathSound "weapons/lpst_impact"
	//BounceSound  // for bounce
	States {
		Spawn:
			BAL2 A 1 Bright
			Loop
		Death:
			BAL2 CDE 2 Bright
		Stop
	}
}