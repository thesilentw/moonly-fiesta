// --------------------------------------------------------------------------
// Chaingun
//
// The rebalanced Chaingun is no longer quite as accurate at long range, and
// now has a minimum burst size of 4 rounds.
//
// --------------------------------------------------------------------------

class RBLChaingun : MFWeapon replaces Chaingun {
	
	Default {
		Weapon.slotNumber 4;
		Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		Weapon.AmmoType "Clip";
		Inventory.PickupMessage "$GOTCHAINGUN";
		Obituary "$OB_MPCHAINGUN";
		Tag "$TAG_CHAINGUN";
	}
	
	States {
		Ready:
			CHGG A 1 A_WeaponReady;
			Loop;
		Deselect:
			CHGG A 1 A_Lower;
			Loop;
		Select:
			CHGG A 1 A_Raise;
			Loop;
		Fire:
			TNT1 A 0 A_GunFlash();
			CHGG ABAB 4 {
				A_PlaySound ("weapons/chngun", CHAN_WEAPON);
				A_FireBullets(0.9, 0.9, -1, 5, flags: FBF_USEAMMO);
				A_AlertMonsters();
			}
			CHGG B 0 A_ReFire;
			Goto Ready;
		Flash:
			CHGF A 4 Bright A_Light1;
			CHGF B 4 Bright A_Light2;
			CHGF A 4 Bright A_Light1;
			CHGF B 4 Bright A_Light2;
			Goto LightDone;
		Spawn:
			MGUN A -1;
			Stop;
	}
}